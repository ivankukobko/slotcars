
#= require helpers/namespace
#= require vendor/raphael
#= require helpers/math/vector

namespace 'slotcars.shared.views'

Vector = helpers.math.Vector

slotcars.shared.views.TrackView = Ember.View.extend

  elementId: 'track-view'
  gameController: null
  _paper: null
  _path: null
  _finishLine: null
  _pointA: null
  _pointB: null

  DASHED_LINE_WIDTH: 3
  ASPHALT_WIDTH: 70
  BORDER_LINE_WIDTH: 75
  BORDER_ASPHALT_WIDTH: 81
  DIRT_WIDTH: 90
  
  ASPHALT_COLOR: '#1E1E1E'
  LINE_COLOR: '#FFF'
  DIRT_COLOR: '#A67B52'

  didInsertElement: ->
    @_paper = Raphael @$()[0], 1024, 768
    @drawTrack @_path
    @drawFinishLine @_pointA, @_pointB

  drawTrack: (path) ->
    # save path when not in DOM to draw it as soon as inserted
    unless @_paper?
      @_path = path
      return

    @_paper.clear()

    @_drawPath path, @DIRT_WIDTH, @DIRT_COLOR
    @_drawPath path, @BORDER_ASPHALT_WIDTH, @ASPHALT_COLOR
    @_drawPath path, @BORDER_LINE_WIDTH, @LINE_COLOR
    @_drawPath path, @ASPHALT_WIDTH, @ASPHALT_COLOR
    @_drawDashedLine path
    
  drawFinishLine: (pointA, pointB) ->
    unless @_paper?
      @_pointA = pointA
      @_pointB = pointB
      return
    
    return unless pointA? and pointB?
    
    x = pointB.x - pointA.x
    y = pointB.y - pointA.y
    
    vector = Vector.create x: x, y: y
    
    @_finishLine.remove() if @_finishLine
    @_finishLine = @_paper.image "<%= asset_path('views/play/finish-line.jpg') %>", pointA.x, pointA.y, 70, 20
    @_finishLine.translate -35, -10
    @_finishLine.rotate vector.clockwiseAngle()
  
  onCarControlsChange: (->
    (jQuery @$()).off 'touchMouseDown'
    (jQuery @$()).off 'touchMouseUp'

    if @gameController.get 'carControlsEnabled'
      (jQuery @$()).on 'touchMouseDown', (event) => @gameController.onTouchMouseDown event
      (jQuery @$()).on 'touchMouseUp', (event) => @gameController.onTouchMouseUp event

  ).observes 'gameController.carControlsEnabled'

  _drawPath: (path, width, color) ->
    path = @_paper.path path;
    path.attr 'stroke', color
    path.attr 'stroke-width', width
    
    path
  
  _drawDashedLine: (path) ->
    path = @_drawPath path, @DASHED_LINE_WIDTH, @LINE_COLOR
    path.attr 'stroke-dasharray', '- '
    path.attr 'stroke-linecap', 'square'